apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'jacoco'
apply plugin: 'idea'

version = '0.3'
project.ext['rootPackage'] = 'net.ihiroky.niotty'
project.ext['encoding'] = 'UTF-8'

// jar.baseName = 'niotty'
sourceCompatibility = 1.6
targetCompatibility = 1.6
[compileJava, compileTestJava].each {
    it.options.encoding = encoding
}
compileJava.options.compilerArgs = ['-Xlint:all']

repositories {
    mavenCentral()
}

configurations {
    doclet
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.5'
    compile 'com.jcraft:jzlib:1.1.3'
    runtime 'ch.qos.logback:logback-classic:1.0.11'
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-core:1.9.5'

    doclet 'org.umlgraph:umlgraph:5.6'
}

checkstyleMain.configFile = file('checkstyle/main.xml')
checkstyleTest.configFile = file('checkstyle/test.xml')
[checkstyleMain, checkstyleTest, findbugsMain, findbugsTest, pmdMain, pmdTest]*.ignoreFailures = true

// Override the javadoc task to use umlgraph doclet.  This depends on the presence
// of GraphViz on the path in your environment (http://www.graphviz.org)
task javadoc(overwrite: true) {
    setDescription('Generates Javadoc API documentation with apiviz diagrams')
    setGroup(JavaBasePlugin.DOCUMENTATION_GROUP)

    doLast {
        ant.javadoc(classpath: configurations.runtime.asPath,
                    sourcepath: 'src/main/java',
                    doctitle: "Niotty API Reference (${version}, ${buildNumber})",
                    encoding: encoding,
                    packagenames: '*',
                    destdir: docsDir,
                    locale: 'en_US',
                    docletpath: configurations.doclet.asPath) {
            doclet(name: 'org.umlgraph.doclet.UmlGraphDoc') {
                param(name: '-verbose')
                param(name: '-inferrel')
                param(name: '-inferdep')
                param(name: '-inferdepvis', value: 'protected')
                param(name: '-qualify')
                param(name: '-postfixpackage')
                param(name: '-hide', value: '(java.*|org.*)')
                param(name: '-collpackages', value: 'java.util.*')
                param(name: '-nodefontsize', value: '9')
                param(name: '-nodefontpackagesize', value: '7')
                param(name: '-link', value: 'http://docs.oracle.com/javase/7/docs/jdk/api/javadoc/doclet/')
                param(name: '-link', value: 'http://docs.oracle.com/javase/7/docs/api/')
            }
        }
    }
}

